# Corrigé du tutoriel

## Article 1

L'impôt sur le revenu pour un individu est défini comme un pourcentage fixe du
revenu de l'individu sur une année.

```catala
déclaration structure Individu:
  donnée revenu contenu argent
  donnée nombre_enfants contenu entier

déclaration champ d'application CalculImpôtRevenu:
  entrée date_courante contenu date
  entrée individu contenu Individu
  entrée territoires_outre_mer contenu booléen
  interne taux_imposition contenu décimal
  résultat impôt_revenu contenu argent

champ d'application CalculImpôtRevenu:
  définition impôt_revenu égal à
    individu.revenu * taux_imposition
```

## Article 2

Le pourcentage fixe mentionné à l'article 1 est égal à 20 %.

```catala
champ d'application CalculImpôtRevenu:
  étiquette article_2
  définition taux_imposition
  sous condition date_courante < |2000-01-01|
  conséquence égal à
    20%

  étiquette article_2
  définition taux_imposition
  sous condition date_courante >= |2000-01-01|
  conséquence égal à
    21%
```

## Article 3

Si l'individu a la charge de 2 enfants ou plus, alors le pourcentage fixe
mentionné à l'article 1 est égal à 15 %.

```catala
champ d'application CalculImpôtRevenu:
  étiquette article_3 exception article_2
  définition taux_imposition
  sous condition individu.nombre_enfants >= 2
  conséquence égal à
    15%
```

## Article 4

Les individus gagnant moins de 10 000 € sont exonérés de l'impôt sur le revenu mentionné
à l'article 1.

```catala
champ d'application CalculImpôtRevenu:
  étiquette article_4 exception article_3
  définition taux_imposition
  sous condition individu.revenu <= 10 000 €
  conséquence égal à
    0%
```

## Article 5

Les individus gagnant plus de 100 000 € sont soumis à un taux d'imposition de
30%, quel que soit leur nombre d'enfants.

```catala
champ d'application CalculImpôtRevenu:
  étiquette article_5 exception article_3
  définition taux_imposition
  sous condition individu.revenu > 100 000 €
  conséquence égal à
    30%
```

## Article 6

Dans les territoires d'outre-mer, le taux d'imposition pour les individus gagnant
plus de 100 000 € spécifié à l'article 5 est réduit à 25 %.

```catala
champ d'application CalculImpôtRevenu:
  étiquette article_6 exception article_5
  définition taux_imposition
  sous condition individu.revenu > 100 000 € et territoires_outre_mer
  conséquence égal à
    25%
```

## Article 7

Lorsque plusieurs individus vivent ensemble, ils sont collectivement soumis à
l'impôt sur le foyer. L'impôt sur le foyer dû est de 10 000 € par individu du foyer,
et la moitié de ce montant par enfant.

```catala
déclaration champ d'application CalculImpôtFoyer:
  entrée individus contenu liste de Individu
  entrée territoires_outre_mer contenu booléen
  entrée date_courante contenu date

  résultat impôt_foyer contenu argent

champ d'application CalculImpôtFoyerIndividuel:
  définition calcul_impôt_revenu.individu égal à
    individu
  définition calcul_impôt_revenu.territoires_outre_mer égal à
    territoires_outre_mer
  définition calcul_impôt_revenu.date_courante égal à
    date_courante

  définition impôt_foyer égal à
    10 000 € * (1,0 + individu.nombre_enfants / 2)

champ d'application CalculImpôtFoyer:
  définition impôt_foyer
  égal à
    somme argent de
      transforme chaque individu parmi individus en
        (
          résultat de CalculImpôtFoyerIndividuel avec {
            -- individu: individu
            -- territoires_outre_mer: territoires_outre_mer
            -- date_courante: date_courante
          }
        ).impôt_foyer
```

## Article 8

Le montant de l'impôt sur le revenu payé par chaque individu peut être déduit de la
part de l'impôt sur le foyer due par cet individu.

```catala
déclaration champ d'application CalculImpôtFoyerIndividuel:
  entrée individu contenu Individu
  entrée territoires_outre_mer contenu booléen
  entrée date_courante contenu date

  calcul_impôt_revenu champ d'application CalculImpôtRevenu

  résultat impôt_foyer contenu argent
  résultat déduction contenu argent

champ d'application CalculImpôtFoyerIndividuel:
  définition déduction égal à
    si calcul_impôt_revenu.impôt_revenu > impôt_foyer alors impôt_foyer
    sinon calcul_impôt_revenu.impôt_revenu
```

## Test

```catala
déclaration champ d'application TestFoyer:
  résultat calcul contenu CalculImpôtFoyer

champ d'application TestFoyer:
  définition calcul égal à
    résultat de CalculImpôtFoyer avec {
      -- individus:
        [
          Individu {
            -- revenu: 15 000 €
            -- nombre_enfants: 0
          };
          Individu {
            -- revenu: 80 000 €
            -- nombre_enfants: 2
          }
        ]
      -- territoires_outre_mer: faux
      -- date_courante: |1999-01-01|
    }
```
